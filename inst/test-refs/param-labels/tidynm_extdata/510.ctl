$PROBLEM RUN# 510 FULL COV MODEL
$INPUT C ID DV AMT II ADDL TIME RATE HT WT CLCR SEX AGE
$DATA  ../poppk_wcovs.csv IGNORE=C
$SUB ADVAN1 TRANS2

$PK
TVCL=THETA(1)*(CLCR/80)**THETA(3)*(AGE/45)**THETA(4)
CL=TVCL*EXP(ETA(1))
TVV=THETA(2)*(WT/70)**THETA(5)*(AGE/45)**THETA(6)
V=TVV*EXP(ETA(2))
S1=V
$ERROR
Y=F + F*ERR(1)
IPRED=F

$THETA
 (0, 13) ;[L/day] CL
 (0, 75) ;[L] V
 (0.001 );[L/day] CL_{CLCR}
$THETA
 (0.001) ;[L/day] CL_{AGE}
 (0.001) ;[L] V_{WT}
 (0.001) ;[L] V_{AGE}

$OMEGA BLOCK(2)
; a comment
(0.04) ;[P] CL
(0.02) ;[R] CL-V
(0.04) ;[P] V

$SIGMA
0.04 ;[P] Residual

;$MSFI=./401.msf
$EST METH=ITS INTERACTION PRINT=5 NITER=20 SIG=3 NOABORT MSFO=./510.msf
$EST METH=1 INTERACTION PRINT=5 MAX=9999 SIG=3 NOABORT MSFO=./510.msf
$EST METH=IMP INTERACTION PRINT=1 NITER=200 EONLY=0 NOABORT SIGL=9 CTYPE=3 MSFO=./510.msf
$EST METH=IMP INTERACTION PRINT=1 NITER=10 ISAMPLE=2000 EONLY=1 NOABORT SIGL=9 MSFO=./510.msf
$COV
$TABLE NOPRINT ONEHEADER FILE=./510_fixed.tab
 ID TIME IPRED EVID ETA1 ETA2
$TABLE NOPRINT ONEHEADER FILE=./510par.tab
 ID TIME ETA1 ETA2 HT WT CLCR SEX AGE EVID

