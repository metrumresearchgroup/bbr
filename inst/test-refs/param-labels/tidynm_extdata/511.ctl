$PROBLEM RUN# 511 FULL COV MODEL
$INPUT C ID DV AMT II ADDL TIME RATE HT WT CLCR SEX AGE
$DATA  ../poppk_wcovs.csv IGNORE=C
$SUB ADVAN3 TRANS4

$PK
TVCL=THETA(1)*(CLCR/80)**THETA(3)*(AGE/45)**THETA(4)
CL=TVCL*EXP(ETA(1))
TVV1=THETA(2)*(WT/70)**THETA(5)*(AGE/45)**THETA(6)
V1=TVV1*EXP(ETA(2))
TVV2=THETA(7)*(WT/70)
V2=TVV2*EXP(ETA(3))
TVQ=THETA(8)*(WT/70)
Q=TVQ*EXP(ETA(4))
S1=V1
S2=V2
$ERROR
Y=F + F*ERR(1)
IPRED=F

$THETA
 (0, 13) ;[L/day] CL
 (0, 75) ;[L] V1
 (0.001 );[L/day] CL_{CLCR}
 (0.001) ;[L/day] CL_{AGE}
 (0.001) ;[L] V_{WT}
 (0.001) ;[L] V_{AGE}
 (0, 100) ;[L] V2
 (0, 10) ;[L/day] Q

$OMEGA BLOCK(2)
(0.04) ;[P] CL
(0.02) ;[R] CL-V
(0.04) ;[P] V

$OMEGA BLOCK(2)
(0.04) ;[P] V2
(0.02) ;[R] V2-Q
(0.04) ;[P] Q

$SIGMA
0.04 ;[P] Residual

;$MSFI=./401.msf
$ESTIMATION MAXEVAL=9999 PRINT=5 METH=1 INT MSF=./511.msf
$COV
$TABLE NOPRINT ONEHEADER FILE=./511.tab
 ID TIME IPRED EVID ETA1 ETA2
$TABLE NOPRINT ONEHEADER FILE=./511par.tab
 ID TIME ETA1 ETA2 HT WT CLCR SEX AGE EVID
